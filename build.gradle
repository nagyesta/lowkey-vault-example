plugins {
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.github.nagyesta'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

configurations {
    lowkey
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.azure:azure-security-keyvault-keys'
    implementation 'com.azure:azure-security-keyvault-secrets'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.github.nagyesta.lowkey-vault:lowkey-vault-testcontainers'
    testImplementation 'com.github.nagyesta.lowkey-vault:lowkey-vault-client'
    lowkey 'com.github.nagyesta.lowkey-vault:lowkey-vault-app'
}

dependencyManagement {
    dependencies {
        dependency 'com.azure:azure-security-keyvault-secrets:4.4.1'
        dependency 'com.azure:azure-security-keyvault-keys:4.4.1'
        dependency 'com.github.nagyesta.lowkey-vault:lowkey-vault-testcontainers:1.2.2'
        dependency 'com.github.nagyesta.lowkey-vault:lowkey-vault-client:1.2.2'
        dependency 'com.github.nagyesta.lowkey-vault:lowkey-vault-app:1.2.2'
    }
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty("spring.profiles.active", project.hasProperty('docker')
            ? 'docker'
            : project.hasProperty('process') ? 'process' : '')
    systemProperty("lowkey-app-jar", configurations.lowkey.asFileTree.singleFile.absoluteFile.toString())
    systemProperty("javax.net.ssl.trustStore", file("$projectDir/src/test/resources/lowkey-vault-keystore.p12"))
    systemProperty("javax.net.ssl.trustStorePassword", "changeit")
    systemProperty("javax.net.ssl.trustStoreType", "PKCS12")
    testLogging.showStandardStreams = true
}
