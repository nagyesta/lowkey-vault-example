plugins {
    alias(libs.plugins.spring.boot)
    id 'java'
    id 'checkstyle'
}

group = 'com.github.nagyesta'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

configurations {
    lowkey
}
tasks.withType(Checkstyle) {
    configProperties = [base_dir: rootDir.toString(), cache_file: file("${buildDir}/checkstyle/cacheFile")]
    reports {
        xml.required.set(false)
        html.required.set(true)
        html.stylesheet resources.text
                .fromFile(rootProject.file('config/checkstyle/checkstyle-stylesheet.xsl') as String)
    }
}
checkstyle.toolVersion = libs.versions.checkstyle.get()

dependencies {
    implementation libs.spring.boot.starter.web
    implementation libs.bundles.azure
    compileOnly libs.lombok
    annotationProcessor libs.spring.boot.configuration.processor
    annotationProcessor libs.lombok
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.lowkey.vault.testcontainers
    testImplementation libs.lowkey.vault.client
    lowkey libs.lowkey.vault.app
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty("spring.profiles.active", project.hasProperty('docker')
            ? 'docker,managed-identity'
            : project.hasProperty('process') ? 'process' : '')
    systemProperty("lowkey-app-jar", configurations.lowkey.asFileTree.singleFile.absoluteFile.toString())
    systemProperty("lowkey-version", libs.versions.lowkeyVault.get())
    systemProperty("javax.net.ssl.trustStore", file("$projectDir/src/test/resources/lowkey-vault-keystore.p12"))
    systemProperty("javax.net.ssl.trustStorePassword", "changeit")
    systemProperty("javax.net.ssl.trustStoreType", "PKCS12")
    // Only needed if Assumed Identity and DefaultAzureCredential is used to simulate IMDS managed identity
    environment "IDENTITY_ENDPOINT", "http://localhost:8080/metadata/identity/oauth2/token"
    environment "IDENTITY_HEADER", "header"
    testLogging.showStandardStreams = true
}
